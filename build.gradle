
/**
 * zigbee4java Gradle build
 *
 * @author Chris Hatton (christopherhattonuk@gmail.com)
 */

import org.codehaus.groovy.tools.StringHelper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def zigbee4javaProfilesArray = StringHelper.tokenizeUnquoted(zigbee4javaProfiles)

group = 'org.bubblecloud.zigbee4java'

apply plugin: 'java'
apply from: 'build-common.gradle'

/* Include build scripts from all profiles selected in 'gradle.properties' */

allprojects {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "http://repository-bubblecloud.forge.cloudbees.com/release/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

dependencies {
    for (zigbee4javaProfile in zigbee4javaProfilesArray) {
        switch (zigbee4javaProfile) {
            case 'android':
                println 'zigbee4java: Including Android build'
                compile project(':zigbee-serial-android')
                compile project(':zigbee-console-android')
                break;

            case 'javase':
                println 'zigbee4java: Including JavaSE build'
                compile project(':zigbee-serial-javase')
                compile project(':zigbee-console-javase')
                break;
        }
    }
}

